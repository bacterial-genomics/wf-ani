/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    gregorysprenger/wf-ani Nextflow configuration file
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

/*
================================================================================
    Workflow information
================================================================================
*/

manifest {
    name            = 'gregorysprenger/wf-ani'
    author          = "Christopher A. Gulvik"
    homePage        = 'https://github.com/gregorysprenger/wf-ani'
    description     = "Perform Average Nucleotide Identity (ANI) on FastA/Genbank files."
    mainScript      = 'main.nf'
    nextflowVersion = '!>=22.04.3'
    version         = '1.0.0dev'
}

/*
================================================================================
    Load configuration files
================================================================================
*/

// Create empty test profile to pass `nf-core lint`
// Will be overwritten when profiles.conf is loaded
profiles { test {} }

// Set parameters to null
// Will be overwritten when params.conf is loaded
params {
    skani_speed                      = null
    fastani_matrix                   = null
    fastani_kmer_size                = null
    skani_max_results                = null
    skani_learned_ani                = null
    skani_estimate_median            = null
    skani_multi_line_fasta           = null
    fastani_fragment_length          = null
    fastani_minimum_fraction         = null
    skani_compression_factor         = null
    skani_confidence_intervals       = null
    skani_output_ani_greater_than    = null
    skani_estimate_mean_after_trim   = null
    skani_marker_compression_factor  = null
    skani_minimum_alignment_fraction = null
}

// Load configuration files
includeConfig "conf/base.config"
includeConfig "conf/params.config"
includeConfig "conf/workflows.config"
includeConfig "conf/modules.config"

// Load nf-core custom profiles from different Institutions
try {
    includeConfig "${params.custom_config_base}/nfcore_custom.config"
} catch (Exception e) {
    System.err.println("WARNING: Could not load nf-core/config profiles: ${params.custom_config_base}/nfcore_custom.config")
}

// Load workflow-specific configuration files
// This code is adapted from: https://github.com/bactopia/bactopia
if (params.workflows.containsKey(params.ani)) {
    if (params.workflows[params.ani].containsKey("is_workflow")) {
        // Main workflow
        params.workflows[params.ani]['includes'].each { it ->
            if (params.workflows[it].containsKey("modules")) {
                // Subworkflows
                params.workflows[it]['modules'].each { module ->
                    includeConfig "${params.workflows[module].path}/params.config"
                }
            } else {
                // For each Module
                includeConfig "${params.workflows[it].path}/params.config"
            }
        }
    }
}

// Load profiles.config after workflow-specific configuration files
includeConfig "conf/profiles.config"

/*
================================================================================
    Nextflow output files
================================================================================
*/

timeline {
    enabled = true
    file    = "${params.tracedir}/execution_timeline_${trace_timestamp}.html"
}
report {
    enabled = true
    file    = "${params.tracedir}/execution_report_${trace_timestamp}.html"
}
trace {
    enabled = true
    file    = "${params.tracedir}/execution_trace_${trace_timestamp}.txt"
}
dag {
    enabled = true
    file    = "${params.tracedir}/pipeline_dag_${trace_timestamp}.html"
}

/*
================================================================================
    Environment variables
================================================================================
*/

// Capture exit codes from upstream processes when piping
process.shell = ['/bin/bash', '-euo', 'pipefail']

// Export these variables to prevent local Python/R libraries from conflicting with those in the container
// The JULIA depot path has been adjusted to a fixed path `/usr/local/share/julia` that needs to be used for packages in the container.
// See https://apeltzer.github.io/post/03-julia-lang-nextflow/ for details on that. Once we have a common agreement on where to keep Julia packages, this is adjustable.
env {
    PYTHONNOUSERSITE = 1
    R_PROFILE_USER   = "/.Rprofile"
    R_ENVIRON_USER   = "/.Renviron"
    JULIA_DEPOT_PATH = "/usr/local/share/julia"
}

/*
================================================================================
    Workflow functions
================================================================================
*/

// Function to get current timestamp
def trace_timestamp = new java.util.Date().format( 'yyyy-MM-dd_HH-mm-ss')

// Function to ensure that resource requirements don't go beyond a maximum limit
def check_max(obj, type) {
    if (type == 'memory') {
        try {
            if (obj.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1)
                return params.max_memory as nextflow.util.MemoryUnit
            else
                return obj
        } catch (all) {
            println "   ### ERROR ###   Max memory '${params.max_memory}' is not valid! Using default value: $obj"
            return obj
        }
    } else if (type == 'time') {
        try {
            if (obj.compareTo(params.max_time as nextflow.util.Duration) == 1)
                return params.max_time as nextflow.util.Duration
            else
                return obj
        } catch (all) {
            println "   ### ERROR ###   Max time '${params.max_time}' is not valid! Using default value: $obj"
            return obj
        }
    } else if (type == 'cpus') {
        try {
            return Math.min( obj, params.max_cpus as int )
        } catch (all) {
            println "   ### ERROR ###   Max cpus '${params.max_cpus}' is not valid! Using default value: $obj"
            return obj
        }
    }
}
